/*
 * Copyright Elasticsearch B.V. and/or licensed to Elasticsearch B.V. under one
 * or more contributor license agreements. Licensed under the Elastic License
 * 2.0 and the Server Side Public License, v 1; you may not use this file except
 * in compliance with, at your election, the Elastic License 2.0 or the Server
 * Side Public License, v 1.
 */
apply plugin: 'c'

var os = org.gradle.internal.os.OperatingSystem.current()

// This task is deliberately not depended upon. Run it manually if you
// modify the native code,
//   ./gradlew :libs:elasticsearch-vec:native:buildNativeLibrary
// then commit the native library, e.g.
//   git commit libs/vec/platform/darwin-aarch64/libvec.dylib
//
// Look at the disassemble:
//  objdump --disassemble-symbols=_dot8s libs/vec/native/build/libs/vec/shared/libvec.dylib

// gcc -shared -fpic -o libvec.so -I src/vec/headers/ src/vec/c/vec.c -O3

tasks.register("buildNativeLibrary") {
  dependsOn tasks.named("copyLib")
}

def platformName = System.getProperty("os.arch");

model {
  platforms {
    aarch64 {
      architecture "aarch64"
    }
    amd64 {
      architecture "x86-64"
    }
  }
  components {
    vec(NativeLibrarySpec) {
      targetPlatform "aarch64"
      targetPlatform "amd64"

      sources {
        c {
          source {
            srcDir "src/vec/c/${platformName}/"
            include "*.c"
          }
          exportedHeaders {
            srcDir "src/vec/headers/"
          }
        }
      }

      binaries.withType(SharedLibraryBinarySpec) {
        cCompiler.args "-O3"
        if (toolChain in Gcc) {
          cCompiler.args "-march=native"
        }
        linker.args "-march=native"
        linker.args "-mcpu=native"
        linker.args "-mtune=native"
      }
    }
  }
}

tasks.register("copyLib", Copy).configure {
  description "Copy native lib"
  from "build/libs/vec/shared/${platformName}/${os.getSharedLibraryName('vec')}"
  def osName = os.getFamilyName();
  if (os.isMacOsX()) {
    osName = "darwin";
  }
  def architecture = platformName;
  if (platformName.equals("amd64")) {
    architecture = "x64";
  }
  if (platformName.equals("i386")) {
    architecture = "x64";
  }
  into "../../native/libraries/static/${osName}-${architecture}";
  dependsOn tasks.named("vec${platformName.capitalize()}SharedLibrary")
}
